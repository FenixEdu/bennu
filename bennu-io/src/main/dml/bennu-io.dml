package org.fenixedu.bennu.io.domain;

public class GenericFile {
	protected Long size;
	protected DateTime creationDate;
	public String filename;
	public String displayName;
	protected String contentType;
	protected String contentKey;
    protected String checksum;
    protected String checksumAlgorithm;
}

public class GroupBasedFile extends GenericFile;

public class FileRawData {
	protected String contentKey;
	protected bytearray content;
}

public class LocalFileToDelete {
	protected String filePath;
}

public class FileStorage {
	protected String name;
}

	public class DomainStorage extends FileStorage {
	}

	public class LocalFileSystemStorage extends FileStorage {
		protected String path;
		protected Integer treeDirectoriesNameLength;
	}

	public class DriveStorage extends FileStorage {
	    protected String driveUrl;
        protected String appId;
        protected String appUser;
        protected String refreshToken;
        protected String storeDirectorySlug;
        protected int fileIdLength;
        protected int folderNameMaxLength;
	}

public class FileStorageConfiguration {
	protected String fileType (REQUIRED);
}

public class FileSupport;

relation BennuFileSupport {
	public FileSupport playsRole fileSupport {
		multiplicity 0..1;
	}
	protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
}

relation FileStorageFile {
	protected FileStorage playsRole storage {
	    multiplicity 1..1;
	}
	protected GenericFile playsRole file {
		multiplicity *;
	}	
}

relation FileStorageConfigurationFileStorage {
	public FileStorage playsRole storage {
        multiplicity 1..1;
	}
    protected FileStorageConfiguration playsRole configuration {
        multiplicity *;
    }
}

relation GenericFiles {
	protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
	}
	protected GenericFile playsRole file {
		multiplicity *;
	}
}

relation GroupBasedFilesGroup {
    protected GroupBasedFile playsRole file {
        multiplicity *;
    }
    protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole group {
        multiplicity 1..1;
    }
}

relation FileRawDatas {
	protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
	}
	protected FileRawData playsRole fileRawData {
		multiplicity *;
	}
}

relation FileStorages {
	protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
	}
	protected FileStorage playsRole fileStorage {
		multiplicity *;
	}
}

relation DefaultFileStorage {
    protected FileSupport playsRole fileSupportAsDefault {
        multiplicity 0..1;
    }
    public FileStorage playsRole defaultStorage {
        multiplicity 1..1;
    }
}

relation FileStorageConfigurations {
    protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
    }
	protected FileStorageConfiguration playsRole configuration {
		multiplicity *;
	}
}

relation LocalFilesToDelete {
    protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
    }
	protected LocalFileToDelete playsRole delete {
		multiplicity *;
	}
}