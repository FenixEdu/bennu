package org.fenixedu.bennu.io.domain;

public class GenericFile {
	protected Long size;
	protected DateTime creationDate;
	public String filename;
	public String displayName;
	protected String contentType;
	protected String contentKey;
}

public class FileRawData {
	protected String contentKey;
	protected bytearray content;
}

public class LocalFileToDelete {
	protected String filePath;
}

public class FileStorage {
	protected String name;
}

	public class DomainStorage extends FileStorage{
	}

	public class LocalFileSystemStorage extends FileStorage{
		protected String path;
		protected Integer treeDirectoriesNameLength;
	}	

public class FileStorageConfiguration {
	protected String fileType;
}

public class FileSupport;

relation BennuFileSupport {
	public FileSupport playsRole fileSupport {
		multiplicity 0..1;
	}
	protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
}

relation FileStorageFile {
	protected FileStorage playsRole storage {
	    multiplicity 1..1;
	}
	protected GenericFile playsRole file {
		multiplicity *;
	}	
}

relation FileStorageConfigurationFileStorage {
	public FileStorage playsRole storage {
        multiplicity 0..1;
	}
	protected FileStorageConfiguration playsRole configuration {
		multiplicity *;
	}
}

relation GenericFiles {
	protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
	}
	protected GenericFile playsRole file {
		multiplicity *;
	}
}

relation FileRawDatas {
	protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
	}
	protected FileRawData playsRole fileRawData {
		multiplicity *;
	}
}

relation FileStorages {
	protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
	}
	protected FileStorage playsRole fileStorage {
		multiplicity *;
	}
}

relation FileStorageConfigurations {
    protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
    }
	protected FileStorageConfiguration playsRole configuration {
		multiplicity *;
	}
}

relation LocalFilesToDelete {
    protected FileSupport playsRole fileSupport {
        multiplicity 1..1;
    }
	protected LocalFileToDelete playsRole delete {
		multiplicity *;
	}
}