package org.fenixedu.bennu.portal.domain;

public class PortalConfiguration {
	public LocalizedString applicationTitle (REQUIRED);
	public LocalizedString htmlTitle (REQUIRED);
	public LocalizedString applicationSubTitle (REQUIRED);
	public LocalizedString applicationCopyright (REQUIRED);
	public String theme (REQUIRED);
	public String supportEmailAddress;
	public String systemEmailAddress;
	public bytearray logo;
	protected String logoChecksum;
	public String logoType;
	public String logoLinkUrl;
	public String logoTooltip;
	public bytearray favicon;
	public String faviconType;
	public String documentationBaseUrl;
}

relation BennuPortalConfiguration {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
    public PortalConfiguration playsRole configuration {
        multiplicity 0..1;
    }
}

public class MenuItem {
	public Integer ord (REQUIRED);
	public LocalizedString title (REQUIRED);
	public LocalizedString description (REQUIRED);
	protected String path (REQUIRED);
	protected String fullPath (REQUIRED);
	public Boolean visible (REQUIRED);
	public String layout;
	public String icon;
}

public class MenuContainer extends MenuItem {
}

public class MenuFunctionality extends MenuItem {
    protected String itemKey;
    protected String provider;
    public String documentationUrl;
}

relation ApplicationMenu {
    protected PortalConfiguration playsRole configuration {
        multiplicity 0..1;
    }
    protected MenuContainer playsRole menu {
        multiplicity 1..1;
    }
}

relation ApplicationSubRoots {
    protected PortalConfiguration playsRole configurationFromSubRoot {
        multiplicity 0..1;
    }
    protected MenuContainer playsRole subRoot {
        multiplicity *;
    }
}

relation MenuItemComposition {
	protected MenuContainer playsRole parent {
        multiplicity 0..1;
	}
	protected MenuItem playsRole child {
		multiplicity *;
	}
}

relation MenuItemGroup {
    protected MenuItem playsRole menuItem {
        multiplicity *;
    }
    protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole group {
        multiplicity 1..1;
    }
}

public class SupportConfiguration {
    public LocalizedString title (REQUIRED);
    public String emailAddress (REQUIRED);
}

relation MenuItemSupport {
    public MenuItem playsRole menuItem {
        multiplicity *;
    }
    public SupportConfiguration playsRole support {
        multiplicity 0..1;
    }
}

relation BennuSupportConfiguration {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
    public SupportConfiguration playsRole supportConfiguration {
        multiplicity *;
    }
}