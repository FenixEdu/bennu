package myorg.domain;

/*
 * @(#)domain_model.dml
 *
 * Copyright 2009 Instituto Superior Tecnico
 * Founding Authors: Jo√£o Figueiredo, Luis Cruz, Paulo Abrantes, Susana Fernandes
 * 
 *      https://fenix-ashes.ist.utl.pt/
 * 
 *   This file is part of the MyOrg web application infrastructure.
 *
 *   MyOrg is free software: you can redistribute it and/or modify
 *   it under the terms of the GNU Lesser General Public License as published
 *   by the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.*
 *
 *   MyOrg is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *   GNU Lesser General Public License for more details.
 *
 *   You should have received a copy of the GNU Lesser General Public License
 *   along with MyOrg. If not, see <http://www.gnu.org/licenses/>.
 * 
 */


enum myorg.domain.RoleType as RoleType;
enum myorg.domain.Theme.ThemeType as ThemeType;


valueType pt.utl.ist.fenix.tools.util.i18n.MultiLanguageString as MultiLanguageString {
    externalizeWith {
        String exportAsString();
    }

    internalizeWith importFromString();
}

valueType pt.utl.ist.fenix.tools.util.Strings as Strings {
    externalizeWith {
        String exportAsString();
    }

    internalizeWith importFromString();
}

valueType myorg.domain.util.ByteArray as ByteArray {
    externalizeWith {
        bytearray getBytes();
    }
}


class MyOrg {
}

class contents.Node {
	String ojbConcreteClass;
	Integer nodeOrder;
}

	class contents.ActionNode extends contents.Node {
		String path;
		String method;
		String linkBundle;
		String linkKey;
	}

class groups.PersistentGroup {
	String ojbConcreteClass;
}

	class groups.AnonymousGroup extends groups.PersistentGroup {
	}

	class groups.AnyoneGroup extends groups.PersistentGroup {
	}

	class groups.UserGroup extends groups.PersistentGroup {
	}

	class groups.People extends groups.PersistentGroup {
	}

		class groups.NamedGroup extends groups.People {
			String groupName;
		}

			class groups.Role extends groups.NamedGroup {
			}

	class groups.UnionGroup extends groups.PersistentGroup {
	}

class scheduler.Task {
	String ojbConcreteClass;
	DateTime lastRun;
}

class scheduler.TaskConfiguration {
	Integer minute;
	Integer hour;
	Integer day;
	Integer month;
	Integer dayofweek;
}

class scheduler.TaskLog {
	DateTime taskStart;
	DateTime taskEnd;
	String output;
	Boolean successful;
}

class scheduler.CustomTaskLog {
    String className;
    String contents;
	DateTime uploaded;
	DateTime taskStart;
	DateTime taskEnd;
	String output;
}

class User {
	String username;
	String password;
	DateTime lastLogoutDateTime;
}

class VirtualHost {
	String hostname;
	Boolean googleSearchEnabled;
	Boolean languageSelectionEnabled;
	Boolean breadCrumbsEnabled;
	MultiLanguageString applicationTitle;
	MultiLanguageString applicationSubTitle;
	MultiLanguageString applicationCopyright;
	String errorPage (REQUIRED);
	String helpLink;
	ByteArray logo;
	ByteArray favicon;
}

class Theme {
	String name;
	String description;
	ThemeType type;
	String screenshotFileName;
}

class index.IndexDirectory {
	String indexForClass;
}

class index.IndexFile {
	String name;
	Long lastModified;
	Long length;
	Integer indexMarker;
}

class index.IndexFileBuffer {
	Integer indexMarker;
	ByteArray buffer;
}

relation MyOrgIndexDirectory {
	MyOrg playsRole myOrg;
	index.IndexDirectory playsRole indexDirectory {
		multiplicity *;
	}	
}
 
relation MyOrgIndexFile {
	MyOrg playsRole myOrg;
	index.IndexFile playsRole IndexFile {
		multiplicity *;
	}	
}

relation MyOrgIndexFileBuffer {
	MyOrg playsRole myOrg;
	index.IndexFileBuffer playsRole indexFilebuffers {
		multiplicity *;
	}	
}

relation IndexDirectoryIndexFile {
	index.IndexDirectory playsRole directory;
	index.IndexFile playsRole indexFiles {
		multiplicity *;
	}	
}

relation IndexFileIndexFileBuffer {
	index.IndexFile playsRole indexFile;
	index.IndexFileBuffer playsRole indexFileBuffers {
		multiplicity *;
	}	
}

relation ThemeMyOrg {
	MyOrg playsRole myOrg;
	Theme playsRole themes {
		multiplicity *;
	}
}

relation VirtualHostTheme {
	VirtualHost playsRole virtualHosts {
		multiplicity *;
	}
	Theme playsRole theme;
}

relation MyOrgUser {
	MyOrg playsRole myOrg;
	User playsRole user {
		multiplicity *;
	}
}

relation MyOrgVirtualHost {
	MyOrg playsRole myOrg;
	VirtualHost playsRole virtualHosts {
		multiplicity *;
	}
}

relation MyOrgNode {
	MyOrg playsRole myOrg;
	contents.Node playsRole nodes {
		multiplicity *;
	}
}

relation VirtualHostTopLevelNode {
	VirtualHost playsRole virtualHost;
	contents.Node playsRole topLevelNodes {
		multiplicity *;
	}
}

relation NodeNode {
	contents.Node playsRole parentNode;
	contents.Node playsRole childNodes {
		multiplicity *;
	}
}

relation MyOrgPersistentGroup {
	MyOrg playsRole myOrg;
	groups.PersistentGroup playsRole persistentGroups {
		multiplicity *;
	}
}

relation MyOrgSystemGroup {
	MyOrg playsRole systemGroupMyOrg;
	groups.PersistentGroup playsRole systemGroups {
		multiplicity *;
	}
}

relation MyOrgUserGroup {
	User playsRole groupOwner;
	groups.PersistentGroup playsRole userGroups {
		multiplicity *;
	}
}

relation PersistentGroupUnionGroup {
	groups.PersistentGroup playsRole persistentGroups {
		multiplicity *;
	}
	groups.UnionGroup playsRole unionGroups {
		multiplicity *;
	}
}

relation UserPeople {
	groups.People playsRole peopleGroups {
		multiplicity *;
	}
	User playsRole users {
		multiplicity *;
	}
}

relation NodePersistentGroup {
	contents.Node playsRole nodes {
		multiplicity *;
	}
	groups.PersistentGroup playsRole accessibilityGroup;
}

relation MyOrTask {
	MyOrg playsRole myOrg;
	scheduler.Task playsRole tasks {
		multiplicity *;
	}
}

relation MyOrTaskConfiguration {
	MyOrg playsRole myOrg;
	scheduler.TaskConfiguration playsRole taskConfigurations {
		multiplicity *;
	}
}

relation MyOrTaskLog {
	MyOrg playsRole myOrg;
	scheduler.TaskLog playsRole taskLogs {
		multiplicity *;
	}
}

relation TaskTaskConfiguration {
	scheduler.Task playsRole task;
	scheduler.TaskConfiguration playsRole taskConfigurations {
		multiplicity *;
	}
}

relation TaskTaskLog {
	scheduler.Task playsRole task;
	scheduler.TaskLog playsRole taskLogs {
		multiplicity *;
	}
}

relation MyOrgCustomTaskLog {
	MyOrg playsRole myOrg;
	scheduler.CustomTaskLog playsRole customTaskLog {
		multiplicity *;
	}
}

